apply plugin: 'com.android.library'

android {
    compileSdkVersion rootProject.ext.android["compileSdkVersion"]
    buildToolsVersion rootProject.ext.android["buildToolsVersion"]

    defaultConfig {
        minSdkVersion rootProject.ext.android["minSdkVersion"]
        targetSdkVersion rootProject.ext.android["targetSdkVersion"]
        versionCode rootProject.ext.android["versionCode"]
        versionName rootProject.ext.android["versionName"]
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile rootProject.ext.dependencies["appcompat-v7"]
    compile rootProject.ext.dependencies["recyclerview"]
    //生命周期监听库
    compile project(':liba-lifecycle-dispatch')
    //通用滚动监听库
    compile project(':liba-list-scroll-helper')
    //等待弹窗库
    compile project(':liba-wait-view-factory')
    //界面状态切换状态库
    compile project(':liba-load-view-factory')
    //图片加载
    compile 'oms.mmc:imageLoader:1.0.0-SNAPSHOT@aar'
    compile project(':liba-async-executor')
}

//是否提交到仓库
def upload = true
//是否为正版
def release = false
//提交到服务器
if (upload) {
    apply plugin: 'maven'
    group = 'oms.mmc'
    version = !release ? android.defaultConfig.versionName + '-SNAPSHOT' : android.defaultConfig.versionName
    task androidSourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.sourceFiles
    }
    artifacts {
        //当前aar 文件名
        archives file('liba-fast-mvc.aar')
        archives androidSourcesJar
    }
    uploadArchives {
        repositories.mavenDeployer {
            println "upload fastMvc version:" + version
            repository(url: MAVEN_R_URL) {
                authentication(userName: MAVEN_D_USERNAME, password: MAVEN_D_PASSWORD)
            }
            snapshotRepository(url: MAVEN_S_URL) {
                authentication(userName: MAVEN_D_USERNAME, password: MAVEN_D_PASSWORD)
            }
            pom.artifactId = "fast-mvc"
        }
    }
}